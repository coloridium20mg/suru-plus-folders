#compdef suru-plus-folders
#
# @author: Sergei Eremenko (https://github.com/SmartFinn)
# @license: MIT license (MIT)
# @link: https://github.com/gusbemacbe/suru-plus-folders

_get_colors() {
	local -a colors=(aubergine black blue bluegrey bordeaux brown canonical cyan darkblue green grey indigo
		manjaro magenta mint orange pink purple red teal vermillion violet white yellow)
	_wanted colors exlp 'color' compadd -- "${colors[@]}"
	return 0
}

_get_themes() {
	local data_dir icons_dir icon_theme
	local -a data_dirs=()
	local -a icons_dirs=(
		"$HOME/.icons"
		"${XDG_DATA_HOME:-$HOME/.local/share}/icons"
	)

	# Get data directories from XDG_DATA_DIRS variable and
	# convert colon-separated list into bash array
	IFS=: read -rA data_dirs <<< "${XDG_DATA_DIRS:-/usr/local/share:/usr/share}"

	for data_dir in "${data_dirs[@]}"; do
		[ -d "$data_dir/icons" ] || continue
		icons_dirs=( "${icons_dirs[@]}" "${data_dir:a}/icons" )
	done

	for icons_dir in "${icons_dirs[@]}"; do
		for icon_theme in "$icons_dir"/*; do
			[ -f "$icon_theme/places/64/folder-blue.svg" ] || continue
			_wanted icon_theme expl 'icon-theme' compadd -- "${icon_theme:t}"
		done
	done
	return 0
}

_arguments -C \
	'(-h --help)'{-h,--help}'[show help message and exit]' \
	'(-V --version)'{-V,--version}'[show version number and exit]' \
	'(-v --verbose)'{-v,--verbose}'[be verbose]' \
	'(-C --color)'{-C,--color}'[change color of folders]:color:_get_colors' \
	'(-D --default)'{-D,--default}'[back to the default color]' \
	'(-R --restore)'{-R,--restore}'[restore the last used color from the config file]' \
	'(-l --list)'{-l,--list}'[show available colors]' \
	'(-t --theme)'{-t,--theme}'[make changes to the specified theme]:icon-theme:_get_themes' \
	'(-u --update-cache)'{-u,--update-cache}'[update icon cache]'

# vim: ft=zsh sw=4 ts=4
